set(TNG_ROOT_DIR ${CMAKE_CURRENT_LIST_DIR})

include(TestBigEndian)
test_big_endian(TNG_INTEGER_BIG_ENDIAN)
include(CheckIncludeFile)
check_include_file(inttypes.h TNG_HAVE_INTTYPES_H)

macro(TNG_GET_SOURCE_LIST VARIABLE)
    include_directories(${TNG_ROOT_DIR}/include)
    set(_tng_compression_sources bwlzh.c bwt.c coder.c dict.c fixpoint.c huffman.c huffmem.c lz77.c merge_sort.c mtf.c rle.c tng_compress.c vals16.c warnmalloc.c widemuldiv.c xtc2.c xtc3.c)
    set(_tng_io_sources tng_io.c md5.c)
    set(${VARIABLE})
    foreach(_file ${_tng_compression_sources})
        list(APPEND ${VARIABLE} ${TNG_ROOT_DIR}/src/compression/${_file})
    endforeach()
    foreach(_file ${_tng_io_sources})
        list(APPEND ${VARIABLE} ${TNG_ROOT_DIR}/src/lib/${_file})
    endforeach()
endmacro()

macro(TNG_SET_SOURCE_PROPERTIES)
    set(_tng_with_zlib OFF)
    set(_tng_std_inttypes_h ${TNG_HAVE_INTTYPES_H})
    set(_curr_var)
    foreach (_arg ${ARGN})
        if (_arg STREQUAL "WITH_ZLIB")
            set(_curr_var with_zlib)
        elseif (_arg STREQUAL "HAVE_INTTYPES_H")
            set(_curr_var std_inttypes_h)
        elseif (_curr_var)
            set(_tng_${_curr_var} ${_arg})
            set(_curr_var "")
        else()
            message(FATAL_ERROR "Invalid argument ${_arg} to TNG_SET_SOURCE_PROPERTIES")
        endif()
    endforeach()
    if (_tng_with_zlib)
        set_property(SOURCE ${TNG_ROOT_DIR}/src/lib/tng_io.c
                     APPEND PROPERTY COMPILE_DEFINITIONS USE_STD_INTTYPES_H)
    endif()
    if (_tng_std_inttypes_h)
        set_property(SOURCE ${TNG_ROOT_DIR}/src/lib/tng_io.c
                     APPEND PROPERTY COMPILE_DEFINITIONS USE_ZLIB)
    endif()
    if (TNG_INTEGER_BIG_ENDIAN)
        set_property(SOURCE ${TNG_ROOT_DIR}/src/lib/md5.c
                     APPEND PROPERTY COMPILE_DEFINITIONS TNG_INTEGER_BIG_ENDIAN)
    endif()
endmacro()
